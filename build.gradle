buildscript {

    ext {
        ktor_version = '1.5.3-SNAPSHOT'
        kotlin_version = '1.4.20'
        serialization_version = '1.0.1'
        gson_version = '2.8.6'
        logback_version = '1.2.1'
        koin_version = '2.1.0'
        kotlin_logging = '1.7.10'
        exposed = '0.21.1'
        hikaricp_version = '3.4.2'
        mysql_connector = '8.0.18'
        flyway_core = '5.2.4'
        flyway_feature = '1.0.0'
        avro4k_version = '1.0.0'

        // infrastructure
        kafka_clients = '2.4.0'
        kafka_avro_serializer = '5.3.0'

        // tests
        jupiter = '5.5.2'
        mockito = '2.2.0'
        kluent_version = '1.61'

        // docker
        shadow_version = '5.0.0'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'application'

group 'com.quick.tor'
version '0.0.1'
mainClassName = "com.quick.tor.ApplicationMainKt"

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url "https://plugins.gradle.org/m2/" }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://packages.confluent.io/maven/' }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

sourceSets {
    all {
        languageSettings {
            useExperimentalAnnotation('com.quick.tor.RequiresTransactionContext')
            useExperimentalAnnotation('com.quick.tor.OptInAnnotation')
        }
    }
    main.kotlin.srcDirs = [ 'src' ]
    main.resources.srcDirs = [ 'resources' ]
}

jar {
    manifest {
        attributes 'Main-Class': 'com.quick.tor.ApplicationMainKt'
        attributes 'Start-Class': 'com.quick.tor.ApplicationMainKt'
    }
}

shadowJar {
    archiveBaseName = 'quicktor'
    archiveClassifier = ''
    archiveVersion = ''
}
